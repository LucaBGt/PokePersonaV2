const LOCALID_SECURITY = 1
const LOCALID_BUGCATCHER = 19
const LOCALID_SABRINA = 20
const LOCALID_CAMERAMAN = 10
const LOCALID_FATHER = 11
const LOCALID_NURSE = 2

//VAR_SABRINAGYM_STATE
//0 = Speak to lady
//1 = Enter
//2 = Fight Sabrina

mapscripts SabrinaGym_MapScripts {
    MAP_SCRIPT_ON_TRANSITION{    
        setrespawn(HEAL_LOCATION_SABRINA_GYM)
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0, 0: SabrinaGym_Init
        //VAR_SABRINAGYM_STATE, 5: SabrinaGym_PresentGymLeader
    ]
}

script SabrinaGym_Init {

    if(var(VAR_SABRINAGYM_STATE) == 0){

    setflag(FLAG_SABRINAGYM_SABRINA_HIDDEN)
    setobjectxy(LOCALID_SABRINA,1,33)
    removeobject(LOCALID_SABRINA)

    delay(30)

    fadescreen(FADE_FROM_BLACK)
    clearflag(FLAG_SYS_PREVENT_MAP_FADE_IN)

    applymovement(OBJ_EVENT_ID_PLAYER, SabrinaGym_PlayerEnter)
    waitmovement(0)

    }

    setvar(VAR_TEMP_0, 1)
    release 
    end

}

script SabrinaGym_Nurse {
    lock

    msgbox("Nurse: Hello there, Challenger!")
    msgbox("May I fresh up your POKéMON?",MSGBOX_YESNO)

    if(var(VAR_RESULT) == 1){

        msgbox("Nurse: Very well!\nThis will only take a moment...")
        closemessage

        applymovement(LOCALID_NURSE,CommonMovement_NurseBow)
        waitmovement(0)

        fadescreen(FADE_TO_BLACK)
        special(HealPlayerParty)
        playfanfare(MUS_HEAL)
	    waitfanfare
        fadescreen(FADE_FROM_BLACK)
        delay(30)
    }

    applymovement(LOCALID_NURSE,CommonMovement_NurseBow)
    msgbox("Nurse: Best of luck to you!",MSGBOX_AUTOCLOSE)
    
    release
    end
}

script SabrinaGym_Security{
    lock
    faceplayer

    switch(var(VAR_SABRINAGYM_STATE)){
        case 0:
            msgbox("Hi, how can I help you?\nOh, you're a GYM CHALLENGER?\lPlease, check in with the SERVICE COUNTER\lto challenge this GYM.\pYou know the drill! ", MSGBOX_AUTOCLOSE)
        break
        default:
            if(flag(FLAG_SABRINAGYM_FRESHWATER)){
            msgbox("Oh yeah, I also participated in\nthe GYM CHALLENGE in my youth.\pAny advice?\nHm...\pNever, ever stop believing in\nyourself and your team.\pEven if things get hard and no-one\nelse does...\pYou mustn't give up and\nwork hard.\lAlways.\pAh, but I digress.\nSorry for blabbering on!", MSGBOX_AUTOCLOSE)
            } else {
            msgbox("Oh, hi there, young\nchamp-in-the-making!", MSGBOX_AUTOCLOSE)
            msgbox("If you're a challenger, I gotta\nwarn ya:\lThe trainers in this GYM are no JOKE!", MSGBOX_AUTOCLOSE)
            msgbox("Here, this is for you.\nKeep your cool!", MSGBOX_AUTOCLOSE)
            giveitem(ITEM_FRESH_WATER)
            setflag(FLAG_SABRINAGYM_FRESHWATER)
            closemessage
            }
        break
    }

    applymovement(LOCALID_SECURITY, CommonMovement_FaceDown)
    waitmovement(0)

    release
    end
}

script SabrinaGym_DontGoIn_Left {
    lockall
    call(SabrinaGym_DontGoIn_1)
    releaseall
    end
}

script SabrinaGym_DontGoIn_Right {
    lockall
    call(SabrinaGym_DontGoIn_1)
    releaseall
    end
}

script SabrinaGym_TraierLeave_Right{
    applymovement(LOCALID_BUGCATCHER, CommonMovement_WalkLeft)
    waitmovement(0)

    call(SabrinaGym_DontGoIn_2)

    applymovement(LOCALID_BUGCATCHER, CommonMovement_FaceRight)
    applymovement(OBJ_EVENT_ID_PLAYER, CommonMovement_FaceLeft)
    waitmovement(0)


    call(SabrinaGym_DontGoIn_3)
}

script SabrinaGym_TraierLeave_Left{
    call(SabrinaGym_DontGoIn_2)

    applymovement(LOCALID_BUGCATCHER, CommonMovement_FaceLeft)
    applymovement(OBJ_EVENT_ID_PLAYER, CommonMovement_FaceRight)
    waitmovement(0)

    call(SabrinaGym_DontGoIn_3)
}

script SabrinaGym_DontGoIn_1{
    applymovement(LOCALID_SECURITY, CommonMovement_EmoteExclamation)
    delay(60)

    applymovement(LOCALID_SECURITY, CommonMovement_FaceRight)
    applymovement(OBJ_EVENT_ID_PLAYER, CommonMovement_FaceDown)
    waitmovement(0)

    delay(30)

    msgbox("Sorry, if you're a visitor, please\nuse the visitor entrances.", MSGBOX_AUTOCLOSE)
    msgbox("Oh?\nYou're a challenger?\pIn that case, please check in at\nthe front desk to get started.", MSGBOX_AUTOCLOSE)

    applymovement(OBJ_EVENT_ID_PLAYER, CommonMovement_WalkDown)
    waitmovement(0)

    //delay(30)

    applymovement(LOCALID_SECURITY, CommonMovement_FaceDown)
    waitmovement(0)
    return
}

script SabrinaGym_DontGoIn_2{

    msgbox("OUT OF MY WAY!\nMOVE!", MSGBOX_AUTOCLOSE)

    applymovement(OBJ_EVENT_ID_PLAYER, CommonMovement_EmoteQuestion)
    delay(60)

    applymovement(LOCALID_BUGCATCHER,SabrinaGym_TrainerMoveDown)
    waitmovement(0)

    //delay(30)

    applymovement(LOCALID_BUGCATCHER, CommonMovement_EmoteExclamation)
    delay(60)

    return
}

script SabrinaGym_DontGoIn_3{

    msgbox("It's crazy! These trainers are\ninsanely strong!\pIt's like they can predict my\nevery move!\pDarn...\nI knew I should've challenged\lANISTAR GYM instead...")

    msgbox("But it's all over now anyway.\nHm? What do you mean?")
    msgbox("Oh, you had no idea?!\nLet me set some IMPORTANT things\lstraight:")
    msgbox("You can't just try IMPORTANT BATTLES\nagain and again, like vs a \lGYM LEADER, for example.")
    msgbox("If you lose, you gotta live with that loss.\nBut, most subordinate trainers will give\lyou tips about their superiors's\lPOKéMON.")
    msgbox("I lost against the GYM LEADER.\nMy GYM CHALLENGE end here.\pI'm going home.")
    closemessage

    applymovement(LOCALID_BUGCATCHER,SabrinaGym_DefeatedTrainer)
    waitmovement(0)


    playse(SE_DOOR)
    removeobject(LOCALID_BUGCATCHER)
    setvar(VAR_SABRINAGYM_STATE,2)
    setflag(FLAG_SABRINAGYM_BUGCATCHER)

    return
}

script SabrinaGym_DontLeave {
    
    lockall
    applymovement(LOCALID_SECURITY, CommonMovement_EmoteExclamation)
    delay(60)

    applymovement(LOCALID_SECURITY, CommonMovement_WalkDown)
    waitmovement(0)

    delay(30)

    msgbox("Sorry, but if you leave the GYM,\nyour challenge will be FORFEIT.\lThere is NO GOING BACK or TRYING AGAIN.\lDo you REALLY want to give up?", MSGBOX_AUTOCLOSE)

    applymovement(LOCALID_SECURITY, CommonMovement_WalkUp)
    applymovement(OBJ_EVENT_ID_PLAYER, CommonMovement_WalkUp)
    waitmovement(0)

    applymovement(LOCALID_SECURITY, CommonMovement_FaceDown)
    waitmovement(0)

    release
    end
}

script SabrinaGym_Announcement_1 {
    lock

    msgbox("???: And who do we have here?")

    applymovement(OBJ_EVENT_ID_PLAYER, CommonMovement_EmoteExclamation)
    delay(60)

    msgbox("Announcer: This young trainer has\njust started the SAFFRON CHALLENGE!")
    msgbox("Let's see how good they do.\nGive them a big round of applause!")
    closemessage

    setvar(VAR_SABRINAGYM_STATE,3)
    release
    end
}

script SabrinaGym_Announcement_2 {
    lock

    applymovement(OBJ_EVENT_ID_PLAYER, CommonMovement_EmoteExclamation)
    delay(60)

    msgbox("Announcer: Wow, Challenger {PLAYER} is\ncoming in HOT!")
    msgbox("But are they strong enough to make it\nto the GYM LEADER?\lOnly time will tell...")
    closemessage

    setvar(VAR_SABRINAGYM_STATE,4)
    release
    end
}

script SabrinaGym_Announcement_3 {
    lock

    applymovement(OBJ_EVENT_ID_PLAYER, CommonMovement_EmoteExclamation)
    delay(60)

    msgbox("Announcer: Challenger {PLAYER} is\nalmost there!")
    msgbox("Victory is close-\nbut the hardest challenge is\lyet to come.")
    closemessage

    setvar(VAR_SABRINAGYM_STATE,5)
    release
    end
}

script SabrinaGym_CounterLady{
    lock
    switch(var(VAR_SABRINAGYM_STATE)){
        case 0:

            msgbox("Hi there, love!\nWelcome to SAFFRON GYM!")
            msgbox("Are you an audience member, or-\nOh! Is that a TRAINER-CARD I see?")
            msgbox("Let me see that for a moment, please...")
            closemessage

            delay(60)

            msgbox("Ah, I see!\nTHREE shining BADGES, and your\lPOKéMON-TEAM is looking mighty\lstrong too!")
            msgbox("I'll let the GYM LEADER know that you'll\nbe contending for your FOURTH BADGE.")
            msgbox("Here, this a participatory gift!")
            closemessage

            giveitem(ITEM_TM46)
            setvar(VAR_SABRINAGYM_STATE, 1)

            msgbox("We'll be cheering you on!",MSGBOX_AUTOCLOSE)
        break
        default:
            msgbox("We'll be cheering you on!",MSGBOX_AUTOCLOSE)
        break
    }
    release
    end
}

script SabrinaGym_PresentGymLeader{

    applymovement(OBJ_EVENT_ID_PLAYER, CommonMovement_EmoteExclamation)
    delay(60)

    msgbox("Announcer: OH MAN, HERE THEY ARE!",MSGBOX_AUTOCLOSE)

    special(SpawnCameraObject)

    applymovement(OBJ_EVENT_ID_CAMERA, SabrinaGym_CamUp)
    waitmovement(0)

    msgbox("Announcer: Well, well, well.",MSGBOX_AUTOCLOSE)

    applymovement(LOCALID_CAMERAMAN,SabrinaGym_CameraMan_1)
    applymovement(LOCALID_FATHER,SabrinaGym_ManDown)
    waitmovement(0)

    msgbox("Congrats on coming this far.\nThe final challenge of SAFFRON GYM\lawaits!",MSGBOX_AUTOCLOSE)

    applymovement(LOCALID_FATHER, CommonMovement_EmoteQuestion)
    delay(60)

    msgbox("Hahaha, no, I'm not the GYM LEADER.\pAhem...",MSGBOX_AUTOCLOSE)

    applymovement(LOCALID_FATHER, CommonMovement_FaceLeft)
    waitmovement(0)

    msgbox("DEAR AUDIENCE!",MSGBOX_AUTOCLOSE)

    applymovement(LOCALID_FATHER, CommonMovement_FaceRight)
    waitmovement(0)

    msgbox("IT'S THE MOMENT YOU'VE BEEN\nWAITING FOR!",MSGBOX_AUTOCLOSE)

    applymovement(LOCALID_FATHER, CommonMovement_FaceDown)
    waitmovement(0)

    msgbox("Please, put your hands together for\nthe LEADER of SAFFRON GYM!\pThe girl of mystery,\nthe mistress of PSYCHIC POKéMON...",MSGBOX_AUTOCLOSE)

    applymovement(LOCALID_FATHER, CommonMovement_WalkRight)
    waitmovement(0)
    applymovement(LOCALID_CAMERAMAN, CommonMovement_FaceLeft)
    applymovement(LOCALID_FATHER, CommonMovement_FaceLeft)
    waitmovement(0)

    msgbox("... my darling daughter.\nSABRINA!",MSGBOX_AUTOCLOSE)

    addobject(LOCALID_SABRINA)
    setobjectxy(LOCALID_SABRINA,15,8)

    playse(SE_DOOR)
    delay(60)

    applymovement(LOCALID_SABRINA,SabrinaGym_ManDown)
    waitmovement(0)

    msgbox("Announcer: Show 'em what you got, honey.")
    msgbox("And at least TRY to smile for the camera,\nokay?",MSGBOX_AUTOCLOSE)

    trainerbattle_single(TRAINER_SABRINA_YOUNG, "...", "...")

}

script SabrinaGym_Boy_1{
    lockall
    faceplayer
    msgbox("Oh man, oh man, oh man...\nI'm so nervous...\pI already lost, but by friend\nis still in there...")
    msgbox("The trainers in the GYM are\ncrazy strong!\lI didn't even make it to the GYM LEADER...", MSGBOX_AUTOCLOSE)
    releaseall
    end
}

script SabrinaGym_Girl_1{
    lockall
    faceplayer
    msgbox("Darn, tickets are all sold out...")
    msgbox("Guess I'll try again tomorrow,\nI just hope the challengers are\las strong then as they are today!", MSGBOX_AUTOCLOSE)
    releaseall
    end
}

script SabrinaGym_Trashcan{
    lock
    msgbox("The trashcan is full of empty\nsoda bottles and popcorn bags.", MSGBOX_SIGN)
    closemessage
    end
}

script SabrinaGym_Adah{
    trainerbattle_single(TRAINER_ADAH, "Welcome to SAFFRON CITY GYM!\nLet's see what you're made of...", "Oh, nicely done!")
    msgbox("Good luck!\nYou'll need it here...")
    msgbox("A hint about the GYM LEADER?\nHm, I guess I owe you since you beat me.")
    msgbox("How about this:\nTheir trademark POKéMON is ALAKAZAM.\lAnd it's real strong. No joke.")
    msgbox("I heard it uses a FIGHTING TYPE\nATTACK to deal with DARK TYPE\lPOKéMON.")

    closemessage
    end
}

script SabrinaGym_Louise{
    trainerbattle_double(TRAINER_LOUISE, "Can I see into the future?\nMaybe!", "I totally saw that coming!", "Sorry, but you'll need more\nPOKéMON to fight me!")
    msgbox("The future isn't yet written...")
    msgbox("Do you think I would have won\nif I used more POKéMON?")
    msgbox("The GYM LEADER fights with 4 POKéMON,\nI thought I could take you on with\ljust 3...")
    closemessage

    end
}

script SabrinaGym_Reina{
    trainerbattle_single(TRAINER_REINA, "POKéMON are our partners,\nin life and in combat!", "Ah, farts.")
    msgbox("What I said before I read in a\nself-help book, but I guess it\ldidn't make me a winner...")
    msgbox("I read too that you can gain the\nadvantage in battle by using\lSTAT-ALTERING moves.")
    msgbox("Its a strategy the GYM LEADER uses\non their ESPEON.\pGet their STATS up and pass them on\nwith BATON PASS.")
    closemessage

    end
}

script SabrinaGym_Gandalf{
    trainerbattle_single(TRAINER_GANDALF, "Hey, come one...\nI'll fight ya, but don't ask\labout me name, OK?", "Aw man...")
    msgbox("Why did my parents hate me?", MSGBOX_AUTOCLOSE)
    end
}

script SabrinaGym_Spike{
    trainerbattle_single(TRAINER_SPIKE, "Taking on all the GYM CHALLENGERS\nis a bunch of work...", "That wasn't my best performance...")
    msgbox("I'm tired...")
    msgbox("Oh, by the way, which POKéMON\ndo you think is better?\pI use BRONZOR, but the GYM LEADER\nuses METANG for STEEL TYPE coverage.")
    msgbox("Even with its use of MAGNET RISE,\nI think my BRONZOR could take their\lMETANG any day!")

    closemessage
    end
}

script SabrinaGym_Paletto{
    trainerbattle_single(TRAINER_PALETTO, "All eyes are on us now...", "Smile and wave,\njust smile and wave.")
    msgbox("Watching the GYM challenge is\na really popular passtime.\lI'm excited to work at the GYM\lduring GYM SEASON.")
    msgbox("Hm... I was really trying to take\nsome pointers from the GYM LEADER...")
    msgbox("Why? Oh, I use WOBUFFET because of them.\nIt's their OPENING POKéMON!\lBut I guess I still have much to learn...")
    closemessage

    end
}

script SabrinaGym_Sabrina_Debug{
    trainerbattle_single(TRAINER_SABRINA_YOUNG, "All eyes are on us now...", "Smile and wave,\njust smile and wave.")
    msgbox("Watching the GYM challenge is\na really popular passtime.\lI'm excited to work at the GYM\lduring GYM SEASON.", MSGBOX_AUTOCLOSE)
    end
}

movement SabrinaGym_PlayerEnter{
    walk_slow_up * 2
}

movement SabrinaGym_TrainerMoveDown {
    walk_fast_down * 6
    delay_16
}

movement SabrinaGym_DefeatedTrainer {
    walk_fast_down * 5
    delay_16
}

movement SabrinaGym_CamUp {
    walk_up * 2
    delay_16
}

movement SabrinaGym_ManDown {
    walk_slow_down * 2
    delay_16
}

movement SabrinaGym_CameraMan_1 {
    walk_left
    face_down
    delay_16
}

movement CommonMovement_NurseBow{
    nurse_joy_bow
    delay_16
    face_down
}