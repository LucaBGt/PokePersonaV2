const LOCALID_AGENT = 1
const LOCALID_FILE = 2

mapscripts InterrogationRoom_MapScripts {
		MAP_SCRIPT_ON_TRANSITION{
        }
		MAP_SCRIPT_ON_RESUME{
			//setobjectxy(LOCALID_AGENT,19,19)
			//setobjectxy(LOCALID_FILE,19,19)

		}
	MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_GAMESTATE, 1: InterrogationRoom_FirstScene
		VAR_GAMESTATE, 3: InterrogationRoom_AfterOak
    ]
}

script InterrogationRoom_FirstScene{
	lockall

	setobjectxy(LOCALID_AGENT,19,19)
	setobjectxy(LOCALID_FILE,19,19)
	applymovement(LOCALID_FILE,CommonMovement_FaceDown)
	applymovement(OBJ_EVENT_ID_PLAYER,CommonMovement_FaceUp)

	delay(30)

	msgbox("???: And so, the game begins.")
	closemessage

	delay(30)

	fadescreen(FADE_FROM_BLACK)
	//clearflag(FLAG_SYS_PREVENT_MAP_FADE_IN)

	special(SpawnCameraObject)

	delay(30)

	applymovement(OBJ_EVENT_ID_CAMERA,InterogationRoom_CameraUp)
	waitmovement(0)

	delay(30)

	playse(SE_DOOR)
	setobjectxy(LOCALID_AGENT,5,7)
	//clearflag(FLAG_SYS_PREVENT_MAP_FADE_IN)
	waitse

	applymovement(LOCALID_AGENT,CommonMovement_FaceRight)
	delay(30)

	applymovement(LOCALID_AGENT,InterogationRoom_AgentSit)
	waitmovement(0)

	delay(30)

	load_field_pic(0, 40, 80)
	msgbox("???: I see you finally decided to talk.")
	msgbox("???: See?\nThis isn't so bad.\pLet us begin.")
	closemessage
	destroy_field_pic(0)

	applymovement(LOCALID_AGENT,CommonMovement_FaceLeft)
	waitmovement(0)
	delay(60)
	applymovement(LOCALID_AGENT,CommonMovement_FaceDown)
	waitmovement(0)

	delay(30)

	setobjectxy(LOCALID_FILE,7,9)
	clearflag(FLAG_HIDE_FILES)

	delay(30)

	load_field_pic(0, 40, 80)
	msgbox("Agent: So.\nA bit of backstory on you.")
	msgbox("Remember, I'm here to help you.\pYou're not getting away if you don't\ncooperate.")
	closemessage
	destroy_field_pic(0)

	applymovement(LOCALID_FILE,InterogationRoom_OpenFile)
	waitmovement(0)

	load_field_pic(0, 40, 80)
	msgbox("Agent: Says here you used to\nbe a POKÃ©MON trainer.")
	msgbox("A quite prolific one,\nat that.")
	closemessage
	destroy_field_pic(0)

	delay(30)
	call(Agent_ShakeHead)

	load_field_pic(0, 40, 80)
	msgbox("Now I'm wondering...\nHow does a person like THAT stoop to\lsuch lows...")
	closemessage
	destroy_field_pic(0)

	playse(SE_DOOR)
	fadescreen(FADE_TO_BLACK)

	applymovement(OBJ_EVENT_ID_CAMERA,InterogationRoom_CameraDown)
	waitmovement(0)
	special(RemoveCameraObject)

	msgbox(format("Agent: You got your start like so many trainers back in the day. How long ago was that?"))
	closemessage
	delay(60)
	msgbox("PROLOGUE\n11 Years ago...",MSGBOX_SIGN)
	closemessage

	setvar(VAR_GAMESTATE, 2)

	setflag(FLAG_SYS_PREVENT_MAP_FADE_OUT)
	warp(MAP_OAK_LAB_INTRO,255,9,20)

	closemessage
	releaseall
	end
}

script InterrogationRoom_AfterOak{

	lock

	clearflag(FLAG_SYS_PREVENT_MAP_FADE_OUT)
    clearflag(FLAG_SYS_PREVENT_MAP_FADE_IN)

	applymovement(OBJ_EVENT_ID_PLAYER,CommonMovement_FaceUp)

	fadescreen(0)

	delay(60)

	msgbox("Agent: So, after that your journey\nreally begun.")
	msgbox("I'm especially interested to hear\nabout your, what was it,\lfourth gym badge.")
	closemessage

	setflag(FLAG_BADGE01_GET)
	setflag(FLAG_BADGE02_GET)
	setflag(FLAG_BADGE03_GET)

	additem(ITEM_SUPER_POTION,4)
	additem(ITEM_POTION,8)
	additem(ITEM_PARALYZE_HEAL,1)
	additem(ITEM_RARE_CANDY,1)
	additem(ITEM_PREMIER_BALL,4)
	additem(ITEM_POKE_BALL,8)

	switch(var(VAR_STARTER_MON)){
        case 0:
			closemessage
			special(SetPartyChikorita1)
        break
        case 1:
			special(SetPartyPiplup1)
        break
        case 2:
			special(SetPartyTepig1)
        break
    }

	special(AddParty1)

	special(HealPlayerParty)
	waitstate

	//setflag(FLAG_SYS_PREVENT_MAP_FADE_OUT)
	//warp(MAP_OAK_LAB_INTRO,255,9,20)

	setvar(VAR_GAMESTATE, 4)

	release
	end
}

movement InterogationRoom_CameraUp {
	walk_up * 2
}

movement InterogationRoom_CameraDown {
	walk_down * 2
}

movement CommonMovement_FaceLeft{
	face_left
}

movement CommonMovement_FaceRight{
	face_right
}

movement CommonMovement_FaceUp{
	face_up
}

movement CommonMovement_FaceDown{
	face_down
}

movement CommonMovement_WalkLeft{
	walk_left
}

movement CommonMovement_WalkRight{
	walk_right
}

movement CommonMovement_WalkUp{
	walk_up
}

movement CommonMovement_WalkDown{
	walk_down
}

movement CommonMovement_EmoteExclamation{
	emote_exclamation_mark
}

movement CommonMovement_EmoteQuestion{
	emote_question_mark
}

movement CommonMovement_FacePlayer{
	face_player
}

movement  InterogationRoom_AgentSit{
	walk_slow_right
	walk_slow_down
	walk_slow_right
	face_down
	delay_16
}

script Agent_ShakeHead{
  applymovement(LOCALID_AGENT, CommonMovement_AgentShakeHead)
  waitmovement(0)

  delay(30)

  return
}

movement CommonMovement_AgentShakeHead{
	emote_anim_frame_one
delay_16
  	emote_anim_frame_two
delay_16
  	emote_anim_frame_one
delay_16
  	emote_anim_frame_two
delay_16
	face_down
  	delay_16
}

movement  InterogationRoom_OpenFile{
	face_down
	delay_16
	face_up
	delay_16
	face_left
	delay_16
}